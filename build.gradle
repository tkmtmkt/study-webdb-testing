defaultTasks 'assemble'

apply plugin: 'java-base'
apply from: "gradle/dependencies.gradle"

allprojects {
  group = 'com.github.tkmtmkt'
  version = '1.0-SNAPSHOT'

  repositories {
    flatDir {
      dirs "$rootDir/lib"
    }
  }
}

project(':ears') {
  subprojects {
    apply plugin: 'ear'
  }
}

configure([project(':jars'), project(':test')]) {
  subprojects {
    if (name.endsWith('WAR')) {
      apply plugin: 'war'
      jar.enabled = false
    } else {
      apply plugin: 'java'
    }

    sourceCompatibility = 8
    targetCompatibility = 8

    def defaultEncoding = 'UTF-8'
    [compileJava, compileTestJava]*.options*.encoding = defaultEncoding

    compileJava {
      options.compilerArgs += ['-Xlint:all']
    }

    javadoc {
      failOnError = false
      options.charSet(defaultEncoding)
    }

    test {
      ignoreFailures = true
      testLogging {
        events 'started', 'skipped', 'passed', 'failed'
      }
    }

    dependencies {
      //プロダクト用（コンパイル、実行、配布）
      compile libraries.slf4j_api

      //プロダクト用（コンパイル）
      compileOnly libraries.javaee_api
      compileOnly libraries.lombok

      //プロダクト用（実行、配布）
      runtime libraries.logback_classic

      //テスト用（コンパイル、実行）
      testCompile libraries.junit
      testCompile libraries.assertj
      testCompile libraries.mockito

      //テスト用（コンパイル）
      testCompileOnly libraries.javaee_api
    }

    task copyDependencies(type: Copy) {
      from configurations.testRuntime {
        exclude {
          it.file.name.startsWith('study-')
        }
      }
      into file("$rootDir/libext")
    }
  }
}
